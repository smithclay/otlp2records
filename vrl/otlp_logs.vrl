# @schema logs
# @description OTLP logs flattened for Arrow RecordBatch
#
# timestamp: timestamp, required, "Event timestamp in milliseconds"
# observed_timestamp: int64, required, "Observed timestamp in milliseconds"
# trace_id: string, "Trace ID hex string"
# span_id: string, "Span ID hex string"
# service_name: string, required, "Service name from resource attributes"
# service_namespace: string
# service_instance_id: string
# severity_number: int32, required, "Severity level 0-24"
# severity_text: string, required, "Severity text label"
# body: string, "Log body as string or JSON"
# resource_attributes: json, "Resource attributes blob"
# scope_name: string, "Instrumentation scope name"
# scope_version: string, "Instrumentation scope version"
# scope_attributes: json, "Scope attributes blob"
# log_attributes: json, "Log record attributes blob"
# @end

# vrl/otlp_logs.vrl - OTLP logs -> flat log event

# Timestamps (nanoseconds -> milliseconds)
.timestamp = nanos_to_micros(.time_unix_nano)
.observed_timestamp = nanos_to_micros(.observed_time_unix_nano)

# Service info from resource attributes
.service_name = get_attr(.resource.attributes, "service.name", "unknown")
.service_namespace = get_attr(.resource.attributes, "service.namespace")
.service_instance_id = get_attr(.resource.attributes, "service.instance.id")

# Severity (default to 0/empty)
.severity_number = int_or_default(.severity_number, 0)
.severity_text = string_or_null(.severity_text)
if .severity_text == null { .severity_text = "" }

# Body - encode complex types as JSON
if is_object(.body) || is_array(.body) {
    .body = encode_json(.body)
} else {
    .body = string_or_null(.body)
}

# IDs - null if empty
.trace_id = string_or_null(.trace_id)
.span_id = string_or_null(.span_id)

# Attribute blobs as JSON
.resource_attributes = json_or_null(.resource.attributes)
.scope_name = string_or_null(.scope.name)
.scope_version = string_or_null(.scope.version)
.scope_attributes = json_or_null(.scope.attributes)
.log_attributes = json_or_null(.attributes)

# Clean up nested structures
.time_unix_nano = null
.observed_time_unix_nano = null
.resource = null
.scope = null
.attributes = null

# Routing
._table = "logs"
